#!/usr/bin/env tclsh
##
## Copyright (c) 2019 Jan Kandziora <jjj@gmx.de>
##
## This source code is licensed under the GNU General Public License,
## Version 3. See the file COPYING for more details.
##

## Load packages.
package require cmdline


##
## Defaults.
##


## Hard coded filetypes.
set filetypes {
	bin application/octet-stream  "Binary data"
	txt text/plain                "Plain Text"
}

## Default sector size. Is updated after reading the superblock.
set sectsize 512


##
## Functions.
##

## Initialize mimetypes mappings.
set mimetypes [dict create]
foreach {extension mimetype name} $::filetypes {
	dict set mimetypes $mimetype $extension
}

## Guess extension from mimetype.
proc extension {mimetype} {
	## This hack is neccessary because most magic files found in the wild have
	## very poor support for file extensions. Mime type support is usually good.
	if {[dict exists $::mimetypes $mimetype]} {
		return [dict get $::mimetypes $mimetype]
	} elseif {[regexp {^.*?/([[:alnum:]]*[-.])?(.*?)(\+.*)?$} $mimetype match ldummy extension rdummy]} {
		return [string tolower $extension]
	} else {
		return "bin"
	}
}


## Readlink helper.
proc readlink {symlink} {
	if {[catch {exec -- readlink -e $symlink} node]} {
		return
	}

	return $node
}


## Investigate inode block.
proc investigateInodeBlock {ag block} {
	## Calculate device block number.
	set dblock [expr {$::agblocks*$ag+$block}]

	## Read the block.
	seek $::fd [expr {$::blocksize*$dblock}]
	set data [read $::fd $::blocksize]

	## Run through all potential inodes in a block.
	for {set boffset 0} {$boffset<$::blocksize} {incr boffset $::inodesize} {
		## Skip if not the magic string of an inode.
		if {[string range $data $boffset $boffset+1] ne "IN"} continue

		## Found. Get inode number.
		binary scan [string range $data $boffset+152 $boffset+159] Wu inode

		## Log each visited inode.
		puts -nonewline stderr [format $::cmformat $inode [expr {100*$::ichecked/double($::icount)}]]
		incr ::ichecked

		## Skip if not the magic string of an unused/deleted inode.
		if {[string range $data $boffset $boffset+7] ne "IN\0\0\3\2\0\0"} continue

		## Get ctime.
		binary scan [string range $data $boffset+48 $boffset+51] Iu ctime

		## Ignore files deleted before the specified time.
		if {$ctime<$::mctime} continue

		## Get output filename.
		set of [file join [dict get $::parameters o] [format "%s_%s" [clock format $ctime -format "%Y-%m-%d-%H-%M"] $inode]]

		## Make a dict of any extents found.
		set extents [dict create]
		for {set ioffset 176} {$ioffset<$::inodesize} {incr ioffset 16} {
			## Get extent.
			set extent [string range $data $boffset+$ioffset [expr {$boffset+$ioffset+15}]]

			## Ignore unused extents.
			if {$extent eq "\0\0\0\0\0\0\0\0\0\0\0\0\0\0\0\0"} continue

			## Get data blocks from extent.
			binary scan $extent B* extbits

			## Ignore preallocated, unwritten extents.
			if {[string index $extbits 0]} continue

			## Get extent information.
			set loffset [expr 0b[string range $extbits 1 54]]
			set aag     [expr 0b[string range $extbits 55 106-$::agblklog]]
			set ablock  [expr 0b[string range $extbits 107-$::agblklog 106]]
			set count   [expr 0b[string range $extbits 107 127]]
			set skip    [expr {$aag*$::agblocks+$ablock}]

			## Silently ignore extents beyond the filesystem. These are clearly bogus.
			if {($skip+$count)>=$::dblocks} continue

			## Silently ignore extents even a 64-bit dd cannot handle. These are most likely bogus.
			if {($::blocksize*$loffset)>=(2**63-1)} continue

			## Set up extent record.
			dict set extents $loffset [dict create skip $skip count $count]
		}

		## Ignore all files without valid extents.
		if {$extents eq {}} continue

		## Ignore all files without any extent starting at loffset zero. These are most likely bogus.
		if {![dict exist $extents 0]} continue

		## Recover first block of loffset zero extent for file type reconstruction. Ignore file if dd reported a problem.
		if {[catch {exec -ignorestderr -- dd if=$::fs of=$of bs=$::blocksize skip=[dict get $extents 0 skip] seek=0 count=1 conv=notrunc status=none}]} continue

		## Reconstruct file extension from file content.
		if {![catch {exec -ignorestderr -- file --brief --mime-type $of {*}$::magicopts} mimetype]} {
			## No error. Guess extension from mimetype.
			set extension [extension $mimetype]

			## Rename the recovered file.
			set rof [format "%s.%s" $of $extension]
			file rename -force $of $rof
		} else {
			## Error. No extension.
			set extension {}

			## No renaming of the recovered file.
			set rof $of
		}

		## Ignore all files with extensions from the ignore list.
		if {$extension in [split [dict get $::parameters i] ,]} {
			file delete -force $rof
			continue
		}

		## Only recover those with extensions from the recover list.
		if {[dict get $::parameters r] ne {} && $extension ni [split [dict get $::parameters r] ,]} {
			file delete -force $rof
			continue
		}

		## Recover all extents.
		dict for {loffset inf} $extents {
			dict with inf {
				## Recover the data from this extent. Ignore extents for which dd reported a problem.
				catch {exec -ignorestderr -- dd if=$::fs of=$rof bs=$::blocksize skip=$skip seek=$loffset count=$count conv=notrunc status=none}
			}
		}

		## Log.
		puts stderr [format $::rmformat $rof]
	}
}


## Traverse through inode tree.
proc traverseInodeTree {ag block} {
	## Read inode tree block.
	seek $::fd [expr {$::blocksize*($::agblocks*$ag+$block)}]
	set data [read $::fd $::blocksize]

	## Set record start index depending on inode btree magic.
	## Ignore any tree of unknown format.
	switch -- [string range $data 0 3] {
		IABT {set index 16}
		IAB3 {set index 56}
		default return
	}

	## Get level and number of records.
	binary scan [string range $data 4 5] Su bb_level
	binary scan [string range $data 6 7] Su bb_numrecs

	## Check if node or leaf.
	if {$bb_level>0} {
		## Node. Run through all pointer records.
		for {set rec 0 ; set index [expr {($::blocksize+$index)/2}]} {$rec<$bb_numrecs} {incr rec ; incr index 4} {
			## Get block number of branch.
			binary scan [string range $data $index $index+3] Iu agi_branch

			## Traverse through branch.
			traverseInodeTree $ag $agi_branch
		}
	} else {
		## Leaf. Run through all leaf records.
		for {set rec 0} {$rec<$bb_numrecs} {incr rec ; incr index 16} {
			## Get start inode number.
			binary scan [string range $data $index $index+3] Iu agi_start

			## Run through all inode records.
			for {set inode 0} {$inode<64} {incr inode $::inopblock} {
				## Get block number.
				set iblock [expr {($agi_start+$inode)/$::inopblock}]

				## Investigate that block for deleted inodes.
				investigateInodeBlock $ag $iblock
			}
		}
	}
}


##
## Main program.
##

## Parse command line options.
if {[catch {set parameters [cmdline::getoptions argv {
	{t.arg ""              "deleted since"}
	{r.arg ""              "list of file extensions to recover"}
	{i.arg "bin"           "list of file extensions to ignore"}
	{o.arg "xfs_undeleted" "target directory for recovered files"}
	{m.arg ""              "magic path passed to the 'file' utility"}
	{l                     "list file extensions understood"}
} {[options] device -- options are:}]} result]} {
	puts stderr $result
	exit 127
}


## Pass magic path to file if specified
if {[dict get $::parameters m] ne {}} {
	set magicopts [list "--magic" [dict get $::parameters m]]
} else {
	set magicopts {}
}


## Check if we should list file extensions understood.
if {[dict get $::parameters l]} {
	## Yes. Get file types understood by the file utility
	if {![catch {exec -ignorestderr -- file -l {*}$magicopts 2>/dev/null} data]} {
		## Default column widths.
		set ewidth 0
		set mwidth 0

		## Setup known extensions.
		set known {}
		foreach {extension mimetype name} $::filetypes {
			lappend known $extension

			## Update maximum column widths for output formatting.
			if {[string length $extension]>$ewidth} {
				set ewidth [string length $extension]
			}
			if {[string length $mimetype]>$mwidth} {
				set mwidth [string length $mimetype]
			}
		}

		## Parse returned filetypes.
		foreach line [split $data \n] {
			## Only care for "Strength" lines.
			if {[regexp {^Strength += +[[:digit:]]+@[[:digit:]]+: (.*) \[(.*)\]$} $line match name mimetype]} {
				## Remember filetypes.
				set extension [extension $mimetype]
				if {$extension ni $known} {
					lappend known $extension
					lappend ::filetypes $extension $mimetype $name
				}

				## Update maximum column widths for output formatting.
				if {[string length $extension]>$ewidth} {
					set ewidth [string length $extension]
				}
				if {[string length $mimetype]>$mwidth} {
					set mwidth [string length $mimetype]
				}
			}
		}

		## Return the filetypes, sorted by extension.
		foreach {extension mimetype name} [lsort -dictionary -stride 3 -index 0 $::filetypes] {
			puts [format "%-${ewidth}s %-${mwidth}s %s" $extension $mimetype $name]
		}
	}

	## Exit program
	exit 0
}


## Get the minimum ctime to consider.
if {[dict get $::parameters t] ne {}} {
	if {[catch {clock scan [dict get $::parameters t]} mctime]} {
		puts stderr "Unable to parse timespec. Please put it in a way Tcl's \[clock scan\] can understand, e.g. -2hours."
		exit 32
	}
} else {
	set mctime 0
}


## Get filesystem to scan from command line.
if {[lindex $argv 0] eq {} || [set fs [readlink [lindex $argv 0]]] eq {}} {
	puts stderr "Please specify a block device or an XFS filesystem image."
	exit 32
}

## Check for the type of the filesystem node.
if {[file type $fs] ni [list blockSpecial file]} {
	## Something else.
	puts stderr "Please specify a block device or an XFS filesystem image."
	exit 32
}

## Get list of mounted filesystems.
if {[catch {exec -- mount} result]} {
	## Error during mount. Fail.
	puts stderr $result
	exit 32
}

## Parse list of mounted filesystems.
foreach line [split $result \n] {
	## Skip non-xfs entries.
	if {[lindex $line 4] ne "xfs"} continue

	## Skip non-matching entries.
	if {[readlink [lindex $line 0]] ne $fs} continue

	## Skip non-read-write filesystems.
	if {"rw" ni [split [string trim [lindex $line 5] "()"] ,]} continue

	## Log.
	puts stderr "[lindex $argv 0] ($fs) is currently mounted read-write. Trying to remount read-only."

	## The rw option has been found. Try to remount read-only.
	set lang $env(LANG)
	set env(LANG) C
	if {[catch {exec -- mount -oremount,ro [lindex $line 2]} err]} {
		switch -glob -- $err {
			{mount: only root can use "--options" option} {
				## Fail if we cannot remount due to missing permissions.
				puts stderr "Remount failed. Root privileges are required to run this command on this filesystem."
				exit 32
			}
			{mount: * mount point is busy.} {
				puts stderr "Remount failed. $err"
				exit 32
			}
			"* mount point not mounted or bad option." {
				## Ignore this error.
			}
			default {
				## Error during mount. Fail.
				puts stderr $err
				exit 32
			}
		}
	}
	set env(LANG) $lang

	## Sucess. Log.
	puts stderr "Remount successful."

	## Ignore multiple mounts of the same filesystem as they all share the same read-only option.
	break
}


## Open filesystem image for binary reading.
if {[catch {open $fs r} fd]} {
	puts stderr "Opening of filesystem failed. $fd"
	exit 32
}

## No character set translation.
fconfigure $fd -translation binary

## Create lost+found directory if nonexistent.
if {[catch {file mkdir [dict get $::parameters o]} err]} {
	puts stderr "Cannot create output directory. $err"
	exit 32
}

## No inodes checked so far.
set ichecked 0

## Read first superblock.
set data [read $fd $sectsize]

## Fail if this isn't an XFS superblock
if {[string range $data 0 3] ne "XFSB"} {
	puts stderr "This isn't an XFS filesystem or filesystem image."
	exit 32
}

## Get xfs configuration from filesystem superblock.
binary scan [string range $data   4   7] Iu blocksize
binary scan [string range $data   8  15] Wu dblocks
binary scan [string range $data  84  87] Iu agblocks
binary scan [string range $data  88  91] Iu agcount
binary scan [string range $data 102 103] Su sectsize
binary scan [string range $data 104 105] Su inodesize
binary scan [string range $data 106 107] Su inopblock
binary scan [string index $data     124] cu agblklog
binary scan [string range $data 128 136] Wu icount

## Set message formats.
set cmformat "Checking  inode %[string length $dblocks]d (%3.0f%%)\r"
set rmformat "Recovered file -> %s"
set dmformat "Done.           %[string length $dblocks]s         "


## Log.
puts stderr "Starting recovery."

## Run through all allocation groups.
for {set ag 0} {$ag<$agcount} {incr ag} {
	## Read inode B+tree information sector of this allocation group.
	seek $fd [expr {$blocksize*$agblocks*$ag+2*$sectsize}]
	set data [read $fd $sectsize]

	## Get allocation group inode root block.
	binary scan [string range $data 20 23] Iu agi_root

	## Start traversal of this allocation group's inode B+Tree with root block.
	traverseInodeTree $ag $agi_root
}


## Print completion message.
puts stderr [format $::dmformat {}]
